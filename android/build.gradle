buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        // Qt injects `qtGradlePluginType` dynamically (application/library), which
        // still relies on classpath-based plugin resolution.
        classpath 'com.android.tools.build:gradle:9.0.1'
    }
}

repositories {
    google()
    mavenCentral()
    maven { url = 'https://jitpack.io' }
}

apply plugin: qtGradlePluginType

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    //noinspection GradleDependency
    implementation 'androidx.core:core:1.16.0'
    implementation 'com.github.mik3y:usb-serial-for-android:3.10.0'
    testImplementation 'junit:junit:4.13.2'
}

def getTimestampStr() { return new Date().format('yyyyMMddHHmmss') }

def loadQgcAndroidConfig() {
    final Properties props = new Properties()
    final List<File> candidates = []
    if (project.hasProperty('QGC_ANDROID_PROPERTIES_FILE')) {
        candidates.add(file(project.property('QGC_ANDROID_PROPERTIES_FILE').toString()))
    }
    final String envPath = System.getenv('QGC_ANDROID_PROPERTIES_FILE')
    if (envPath != null && !envPath.isEmpty()) {
        candidates.add(file(envPath))
    }

    // Default: look in the parent of the Gradle project dir (CMAKE_BINARY_DIR)
    // androiddeployqt copies this build.gradle into <build>/android-build/,
    // while CMake generates the properties file in <build>/.
    candidates.add(new File(project.projectDir.parentFile, 'qgc-android-config.properties'))

    for (File candidate : candidates) {
        if (!candidate.exists()) {
            continue
        }

        candidate.withInputStream { stream -> props.load(stream) }
        println "qgcAndroidConfig: ${candidate.absolutePath}"
        final List<String> requiredKeys = [
            'QGC_ANDROID_COMPILE_SDK_VERSION',
            'QGC_ANDROID_TARGET_SDK_VERSION',
            'QGC_ANDROID_MIN_SDK_VERSION'
        ]

        for (String key : requiredKeys) {
            final String value = props.getProperty(key)
            if (value == null || value.isEmpty()) {
                throw new GradleException("qgcAndroidConfig missing required key '${key}' in ${candidate.absolutePath}")
            }
        }

        return props
    }

    throw new GradleException(
        "Missing qgc-android-config.properties. Configure with CMake (qt-cmake) " +
        "or pass -PQGC_ANDROID_PROPERTIES_FILE=/path/to/qgc-android-config.properties"
    )
}

final Properties qgcAndroidConfig = loadQgcAndroidConfig()

def qgcConfigOrDefault = { String key, Object fallback ->
    final String value = qgcAndroidConfig.getProperty(key)
    if (value != null && !value.isEmpty()) {
        return value
    }
    return fallback
}

def resolveCpmJavaSourceDir = {
    final List<File> candidates = []
    final String sharedConfigPath = qgcAndroidConfig.getProperty('QGC_CPM_JAVA_SRC_DIR')
    if (sharedConfigPath != null && !sharedConfigPath.isEmpty()) {
        candidates.add(file(sharedConfigPath))
    }
    if (project.hasProperty('QGC_CPM_JAVA_SRC_DIR')) {
        candidates.add(file(project.property('QGC_CPM_JAVA_SRC_DIR').toString()))
    }
    if (project.hasProperty('cpmJavaSrcDir')) {
        candidates.add(file(project.property('cpmJavaSrcDir').toString()))
    }
    final String envPath = System.getenv('QGC_EXTRA_JAVA_SOURCES_DIR')
    if (envPath != null && !envPath.isEmpty()) {
        candidates.add(file(envPath))
    }

    for (File candidate : candidates) {
        if (candidate.exists()) {
            return candidate
        }
    }

    return null
}

android {
    /*******************************************************
     * The following variables:
     * - androidBuildToolsVersion,
     * - androidCompileSdkVersion
     * - qtAndroidDir - holds the path to qt android files
     *                   needed to build any Qt application
     *                   on Android.
     * - qtGradlePluginType - whether to build an app or a library
     *
     * are defined in gradle.properties file. This file is
     * updated by QtCreator and androiddeployqt tools.
     * Changing them manually might break the compilation!
     *******************************************************/

    // namespace = androidPackageName
    namespace = "org.mavlink.qgroundcontrol"
    compileSdk qgcConfigOrDefault('QGC_ANDROID_COMPILE_SDK_VERSION', androidCompileSdkVersion).toInteger()
    buildToolsVersion androidBuildToolsVersion
    ndkVersion = androidNdkVersion

    // Extract native libraries from the APK
    // packagingOptions.jniLibs.useLegacyPackaging = true

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = [qtAndroidDir + '/src', 'src', 'java'].tap {
                // CPM Java dependencies deployed to the build tree by CMake
                def cpmJava = resolveCpmJavaSourceDir()
                if (cpmJava != null) {
                    it.add(cpmJava.absolutePath)
                    println "cpmJavaSrcDir: ${cpmJava.absolutePath}"
                } else {
                    println "cpmJavaSrcDir: <not found>"
                }
            }
            // Exclude JUnit test sources from the main source set â€” they are
            // picked up via 'src' above but belong to the test configuration.
            java.filter.exclude 'test/**'
            aidl.srcDirs = [qtAndroidDir + '/src', 'src', 'aidl']
            res.srcDirs = [qtAndroidDir + '/res', 'res']
            resources.srcDirs = ['resources']
            renderscript.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
       }
        test {
            java.srcDirs = ['src/test/java']
        }
    }

    tasks.withType(JavaCompile) {
        options.incremental = true
        options.compilerArgs += ['-Xlint:all'] // deprecation
    }

    buildFeatures {
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lint {
        abortOnError = false
        checkReleaseBuilds = true
        // Keep lint strict for project sources but ignore third-party CPM Java
        // code checked out under ../extra_java_sources.
        lintConfig = file("lint.xml")
    }

    // Do not compress Qt binary resources file
    aaptOptions {
        noCompress 'rcc'
    }

    defaultConfig {
        minSdk qgcConfigOrDefault('QGC_ANDROID_MIN_SDK_VERSION', qtMinSdkVersion).toInteger()
        targetSdk qgcConfigOrDefault('QGC_ANDROID_TARGET_SDK_VERSION', qtTargetSdkVersion).toInteger()
        ndk.abiFilters = qtTargetAbiList.split(",")
        if (qgcAndroidConfig.getProperty('QGC_ANDROID_VERSION_CODE')) {
            versionCode Integer.parseInt(qgcAndroidConfig.getProperty('QGC_ANDROID_VERSION_CODE'))
        }
        if (qgcAndroidConfig.getProperty('QGC_ANDROID_VERSION_NAME')) {
            versionName qgcAndroidConfig.getProperty('QGC_ANDROID_VERSION_NAME')
        }

        println "timestamp: " + getTimestampStr()
        // println "applicationId: ${applicationId}"
        println "minSdk: ${minSdk}"
        println "targetSdk: ${targetSdk}"
        println "ndk.abiFilters: ${ndk.abiFilters}"
        println "versionCode: ${versionCode}"
        println "versionName: ${versionName}"
    }

    // println "androidPackageName: ${namespace}"
    println "compileSdk: ${compileSdk}"
    println "androidBuildToolsVersion: ${buildToolsVersion}"
    println "androidNdkVersion: ${ndkVersion}"
    println "project.name: ${project.name}"
    println "buildDir: ${buildDir}"
    println "qtAndroidDir: ${qtAndroidDir}"
    println "Java Version: " + JavaVersion.current()

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        // exclude 'lib/*/*_qmltooling_*'
        exclude 'lib/*/*_Controls_FluentWinUI3_*'
        exclude 'lib/*/*_Controls_Fusion_*'
        exclude 'lib/*/*_Controls_Imagine_*'
        exclude 'lib/*/*_Controls_Material_*'
        exclude 'lib/*/*_Controls_Universal_*'
    }
}
